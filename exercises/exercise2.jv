pipeline BahnhofPipeline {
    block BahnCSVExtractor oftype CSVExtractor{
        url: "https://download-data.deutschebahn.com/static/datasets/haltestellen/D_Bahnhof_2020_alle.CSV";
        delimiter: ";";
    }

    block BahnhofColumnDeleter oftype ColumnDeleter {
        delete: [column J];
    }

    constraint VerkehrAllowedList on text:
        value in [
            "FV", "RV", "nur DPN",
        ];

    valuetype verkehr oftype text {
        constraints: [
            VerkehrAllowedList,
        ];
    }
    
    constraint LaengeAndBreiteAllowedRange on decimal:
        value >= -90 and value <= 90;
    
    valuetype laengeandbreite oftype decimal {
        constraints: [
            LaengeAndBreiteAllowedRange,
        ];
    }

    constraint IFOPTAllowedFormat oftype RegexConstraint {
        regex: /\w{2}:\d*:\d*(?::\d*)?/;
    }

    valuetype ifopt oftype text {
        constraints: [
            IFOPTAllowedFormat,
        ];
    }

    constraint ValidValues oftype LengthConstraint {
        minLength: 1;
        maxLength:100;
    }

    valuetype validtext oftype text{
        constraints: [
            ValidValues,
        ];
    }



    block BahnhofTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "EVA_NR" oftype integer,
            "DS100" oftype validtext,
            "IFOPT" oftype ifopt,
            "NAME" oftype validtext,
            "Verkehr" oftype verkehr,
            "Laenge" oftype laengeandbreite,
            "Breite" oftype laengeandbreite,
            "Betreiber_Name" oftype validtext,
            "Betreiber_Nr" oftype integer,
            "Status" oftype validtext
        ];
    }


    block BahnhofLoader oftype SQLiteLoader {
        table: "trainstops";
        file: "./data/trainstops.sqlite";
    }

    BahnCSVExtractor
        -> BahnhofColumnDeleter
        -> BahnhofTableInterpreter
        -> BahnhofLoader;
} 